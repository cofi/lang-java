module org/sugarj/java/Renaming
imports
  libstratego-lib
  org/sugarj/languages/Java
  org/sugarj/languages/SugarJ

rules
  
  rename-decl(|renamings) = 
    rename-sugardec(|renamings) <+ 
    rename-transdec(|renamings) <+ 
    rename-modeldec(|renamings) <+
    rename-editordec(|renamings) <+
    rename-languagedec(|renamings) <+
    id
    
  rename-sugardec(|renamings) = oncetd(SugarDecHead(id, Id(do-rename(|renamings))))  
  rename-transdec(|renamings) = oncetd(TransDecHead(id, Id(do-rename(|renamings))))
  rename-modeldec(|renamings) = oncetd(ModelDecHead(id, Id(do-rename(|renamings)), id))
  rename-editordec(|renamings) = oncetd(EditorServicesDecHead(id, Id(do-rename(|renamings))))

  rename-languagedec(|renamings) = topdown(try(rename-java(|renamings)))
  
  do-rename(|renamings) :
    from -> to
    where <hashtable-get(|<strip-annos> from)> renamings => to
  
rules
  rename-java(|renamings) = ClassDecHead(id, Id(do-rename(|renamings)), id, id, id)
    
  rename-java(|renamings) = InterfaceDecHead(id, Id(do-rename(|renamings)), id, id)

  rename-java(|renamings) = EnumDecHead(id, Id(do-rename(|renamings)), id) 
    
  rename-java(|renamings) = AnnoDecHead(id, Id(do-rename(|renamings)))
    
  rename-java(|renamings) = ConstrDecHead(id, id, Id(do-rename(|renamings)), id, id)

  rename-java(|renamings) = TypeName(Id(do-rename(|renamings))) 

  rename-java(|renamings) :
       TypeName(qual, Id(from))
    -> TypeName(qual, Id(to))
  where
    <pkg-list> qual => pkgs; 
    <hashtable-get(|<bottomup(strip-annos)> (pkgs, from))> renamings => to 

  rename-java(|renamings) = PackageOrTypeName(Id(do-rename(|renamings)))
  
  rename-java(|renamings) :
       PackageOrTypeName(qual, Id(from))
    -> PackageOrTypeName(qual, Id(to))
  where
    <pkg-list> qual => pkgs; 
    <hashtable-get(|<bottomup(strip-annos)> (pkgs, from))> renamings => to 

  rename-java(|renamings) = AmbName(Id(do-rename(|renamings)))

rules
  pkg-list : PackageOrTypeName(Id(pkg)) -> [pkg]
  pkg-list : PackageOrTypeName(rest, Id(pkg)) -> <conc> (<pkg-list> rest, [pkg])
